grammar mm.ecxt.MMLanguage hidden(WS)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate mmLanguage "urn:marnix:mm.exct/MMLanguage"

MMDatabase:
	(statements+=Statement)*;
	
Statement:
	CommentStatement |
	VariableStatement |
	DvrStatement |
	LabeledStatement |
	Block;

LabeledStatement:
	FloatingHypothesisStatement |
	EssentialHypothesisStatement |
	AxiomStatement |
	ProofStatement;

CommentStatement:
	DOLLAR_C (symbols+=MATHSYMBOL)+ DOLLAR_DOT;
	
VariableStatement:
	DOLLAR_V (symbols+=MATHSYMBOL)+ DOLLAR_DOT;
	
DvrStatement:
	DOLLAR_D (symbols+=MATHSYMBOL)+ DOLLAR_DOT;
	
FloatingHypothesisStatement:
	label=LABEL DOLLAR_F constant=MATHSYMBOL variable=MATHSYMBOL DOLLAR_DOT;
	
EssentialHypothesisStatement:
	label=LABEL DOLLAR_E (symbols+=MATHSYMBOL)+ DOLLAR_DOT;
	
AxiomStatement:
	label=LABEL DOLLAR_A (symbols+=MATHSYMBOL)+ DOLLAR_DOT;
	
ProofStatement:
	label=LABEL DOLLAR_P (symbols+=MATHSYMBOL)+ '$=' proof=(NormalProof|CompressedProof) DOLLAR_DOT;
	
NormalProof:
	(labels+=(LABEL|QUESTIONMARK))+;
	
CompressedProof:
	PARENOPEN (labels+=LABEL)* PARENCLOSE (parts+=COMPRESSEDPART)+;
	
Block:
	'${' contents=MMDatabase '$}';
	
MATHSYMBOL: PARENOPEN | PARENCLOSE | QUESTIONMARK | COMPRESSED | TLABEL | WORD;
LABEL: COMPRESSED | TLABEL;
COMPRESSEDPART: QUESTIONMARK | COMPRESSED;
    
terminal WS : (' '|'\t'|'\r'|'\n')+ | '$(' -> '$)';

terminal DOLLAR_C: '$c';
terminal DOLLAR_V: '$v';
terminal DOLLAR_D: '$d';
terminal DOLLAR_F: '$f';
terminal DOLLAR_E: '$e';
terminal DOLLAR_A: '$a';
terminal DOLLAR_P: '$p';
terminal DOLLAR_DOT: '$.';

terminal PARENOPEN: '(';
terminal PARENCLOSE: ')';
terminal QUESTIONMARK: '?';
terminal COMPRESSED: ('A'..'Z'|'?')+;
terminal TLABEL: ('A'..'Z'|'a'..'z'|'0'..'9'|'-'|'_'|'.')+;
terminal WORD: ('!'..'#'|'%'..'~')+;