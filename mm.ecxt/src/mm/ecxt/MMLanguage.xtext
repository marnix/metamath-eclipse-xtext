grammar mm.ecxt.MMLanguage hidden(WS, COMMENT)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate mmLanguage "urn:marnix:mm.exct/MMLanguage"


MMDatabase:
	(statements+=Statement)*;
	
	
Statement:
	ConstantStatement |
	VariableStatement |
	DvrStatement |
	LabeledStatement |
	Block;

LabeledStatement:
	FloatingHypothesisStatement |
	EssentialHypothesisStatement |
	AxiomStatement |
	ProofStatement;

ConstantStatement:
	DOLLAR_C (constants+=ConstDecl)+ DOLLAR_DOT;
	
VariableStatement:
	DOLLAR_V (variables+=VarDecl)+ DOLLAR_DOT;

DvrStatement:
	DOLLAR_D (variables+=[VarDecl|MATHSYMBOL])+ DOLLAR_DOT;
	
FloatingHypothesisStatement:
	name=LABEL DOLLAR_F constant=[ConstDecl|MATHSYMBOL] variable=[VarDecl|MATHSYMBOL] DOLLAR_DOT;
	
EssentialHypothesisStatement:
	name=LABEL DOLLAR_E (symbols+=[Decl|MATHSYMBOL])+ DOLLAR_DOT;
	
AxiomStatement:
	name=LABEL DOLLAR_A (symbols+=[Decl|MATHSYMBOL])+ DOLLAR_DOT;
	
ProofStatement:
	name=LABEL DOLLAR_P (symbols+=[Decl|MATHSYMBOL])+ DOLLAR_EQUALS proof=(NormalProof|CompressedProof) DOLLAR_DOT;
	
NormalProof:
	(labels+=[LabeledStatement|LABEL] | labels+=[LabeledStatement|QUESTIONMARK])+;
	
CompressedProof:
	PARENOPEN (labels+=[LabeledStatement|LABEL])* PARENCLOSE (parts+=COMPRESSEDPART)+;
	
Block:
	DOLLAR_BRACEOPEN contents=MMDatabase DOLLAR_BRACECLOSE;


Decl: ConstDecl | VarDecl;

ConstDecl returns ConstDecl: name=MATHSYMBOL;

VarDecl returns VarDecl: name=MATHSYMBOL;
		
	
MATHSYMBOL: PARENOPEN | PARENCLOSE | QUESTIONMARK | COMPRESSED | TLABEL | WORD;
LABEL: COMPRESSED | TLABEL;
COMPRESSEDPART: QUESTIONMARK | COMPRESSED;
    
terminal WS : (' '|'\t'|'\r'|'\n')+;
terminal COMMENT : DOLLAR_PARENOPEN -> DOLLAR_PARENCLOSE;

terminal DOLLAR_C: '$c';
terminal DOLLAR_V: '$v';
terminal DOLLAR_D: '$d';
terminal DOLLAR_F: '$f';
terminal DOLLAR_E: '$e';
terminal DOLLAR_A: '$a';
terminal DOLLAR_P: '$p';
terminal DOLLAR_DOT: '$.';
terminal DOLLAR_EQUALS: '$=';
terminal DOLLAR_PARENOPEN: '$(';
terminal DOLLAR_PARENCLOSE: '$)';
terminal DOLLAR_BRACEOPEN: '${';
terminal DOLLAR_BRACECLOSE: '$}';

terminal PARENOPEN: '(';
terminal PARENCLOSE: ')';
terminal QUESTIONMARK: '?';
terminal COMPRESSED: ('A'..'Z'|'?')+;
terminal TLABEL: ('A'..'Z'|'a'..'z'|'0'..'9'|'-'|'_'|'.')+;
terminal WORD: ('!'..'#'|'%'..'~')+;